buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
	id "com.wynprice.cursemaven" version "1.2.3"
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

apply plugin: 'net.minecraftforge.gradle.forge'

version = "v1.8.20.2"
group= "net.geforcemods.securitycraft"
archivesBaseName = "securitycraft"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12.2-14.23.5.2826"
    runDir = "run"
    mappings = "stable_39"
}

repositories {
  maven { //HWYLA
    name = "TehNut Maven"
    url = "http://maven.tehnut.info"
  }
  maven { //main jei maven
    name = "Progwml6 maven"
    url = "http://dvs1.progwml6.com/files/maven"
  }
  maven { //jei fallback
    name = "ModMaven"
    url = "modmaven.k-4u.nl"
  }
  maven { // TOP
	name 'tterrag maven'
	url "http://maven.tterrag.com/"
  }
}

dependencies {
    deobfCompile "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"
	deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.28-17"
    compileOnly curse.resolve("cyclic", "2883487")
    compileOnly curse.resolve("icbm-classic", "2918052")
    compileOnly curse.resolve("lycanites-mobs", "2935745")
    runtime 'mezz.jei:jei_1.12.2:4.16.1.302'
    deobfProvided 'mezz.jei:jei_1.12.2:4.16.1.302:api'
}

processResources
{
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    
    //minify json resources
    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
            File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
        }
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'securitycraft_at.cfg'
    }
}
